
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступныеТипыДанных = СпецификацияREST.ПолучитьДоступныеТипыДанныхПараметра();
	Элементы.ПараметрыURLТипДанных.СписокВыбора.ЗагрузитьЗначения(ДоступныеТипыДанных);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	//
	БуферПараметрыURL = Новый Соответствие;
	
	Для Каждого Элемент Из Объект.ПараметрыURL Цикл
		
		СтруктураДанных = Новый Структура;
		
		СтруктураДанных.Вставить("КраткоеОписание", Элемент.КраткоеОписание);
		СтруктураДанных.Вставить("ТипДанных", Элемент.ТипДанных);
		
		БуферПараметрыURL.Вставить(Элемент.ИмяПараметра, СтруктураДанных);
		
	КонецЦикла;
	
	//
	Объект.ПараметрыURL.Очистить();
	
	//
	МассивПараметров = НайтиПараметрыURL(Объект.Код);
	
	Для Каждого Элемент Из МассивПараметров Цикл
		
		НоваяСтрока = Объект.ПараметрыURL.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
		
		//
		БуферныеДанные = БуферПараметрыURL.Получить(НоваяСтрока.ИмяПараметра);
		
		Если ЗначениеЗаполнено(БуферныеДанные) Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, БуферныеДанные);
		КонецЕсли;
		
	КонецЦикла;
	
	//
	Объект.ЕстьПараметрыURL = (Объект.ПараметрыURL.Количество() > 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция НайтиПараметрыURL(URL)
	
	Результат = Новый Массив;
	
	Коллекция = СтрРазделить(URL, "/", Ложь);
	
	ии = 0;
	
	Для Каждого Элемент Из Коллекция Цикл
		
		Если Лев(Элемент, 1) = "{" И Прав(Элемент, 1) = "}" Тогда
			
			ИмяПараметра = Сред(Элемент, 2, СтрДлина(Элемент) - 2);
			
			Данные = Новый Структура;
			Данные.Вставить("ПорядковыйНомер", ии);
			Данные.Вставить("ИмяПараметра", ИмяПараметра);
			
			Результат.Добавить(Данные);
			
		КонецЕсли;
		
		ии = ии + 1;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти